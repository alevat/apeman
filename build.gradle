buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctor_gradle_plugin_version}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependency_management_plugin_version}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
        classpath "org.junit.platform:junit-platform-gradle-plugin:${junit_platform_version}"
    }
}

repositories {
    jcenter()
}

apply plugin: 'org.asciidoctor.convert'

allprojects {
    group = 'com.alevat.apeman'
    version = '0.0.1'
}

task test

def javaProjects = subprojects.findAll {it.project.path.startsWith(':components:java')}

configure(javaProjects) {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = java_source_compatibility
    targetCompatibility = java_target_compatibility

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }
    }
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile "com.google.guava:guava:${guava_version}"

        testCompile"org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
    }
    junitPlatform {
        engines {
             include 'junit-jupiter'
        }
        tags {
            exclude 'slow'
        }
        includeClassNamePattern '.*Test'
        platformVersion junit_platform_version
    }
}

asciidoctor {
    sourceDir = file('docs')
    sources {
        include '/**/*.adoc'
    }
    outputDir = file('build/docs')
}
