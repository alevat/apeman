buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "gradle.plugin.com.palantir:jacoco-coverage:${jacoco_coverage_version}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependency_management_plugin_version}"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctor_gradle_plugin_version}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
    }
}

repositories {
    jcenter()
}

apply plugin: 'org.asciidoctor.convert'

allprojects {
    group = 'com.alevat.apeman'
    version = '0.0.1'
}

task test

def javaProjects = subprojects.findAll {
    it.project.path.startsWith(':components:java') || it.project.name == 'bdd'
}

configure(javaProjects) {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply from:"${rootDir}/gradle/code-analysis.gradle"
    apply from:"${rootDir}/gradle/code-coverage.gradle"

    sourceCompatibility = java_source_compatibility
    targetCompatibility = java_target_compatibility

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }
    }
    dependencies {
        compile "com.google.guava:guava:${guava_version}"
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile 'org.springframework.boot:spring-boot-starter-web'

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile "junit:junit:${junit_version}"
    }
}

asciidoctor {
    separateOutputDirs = false
    sourceDir = file('docs-src')
    sources {
        include '/**/*.adoc'
    }
    outputDir = file('docs')
}
